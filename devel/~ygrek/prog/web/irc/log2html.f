\ $Id: log2html.f,v 1.2 2007/11/06 20:35:51 ygreks Exp $
\ Конвертор IRC логов в HTML

REQUIRE STR@ ~ac/lib/str5.f
REQUIRE SPLIT ~pinka/samples/2005/lib/split.f
REQUIRE CONT ~profit/lib/bac4th.f
REQUIRE PARSE-IRC-MSG ~ygrek/lib/net/irc/basic.f
REQUIRE ENSURE ~ygrek/lib/debug/ensure.f
REQUIRE FileLines=> ~ygrek/lib/filelines.f

: put-header ( a u -- )
" <!DOCTYPE HTML PUBLIC {''}-//W3C//DTD HTML 4.0 Transitional//EN{''}>
<HTML>
<HEAD>
<META HTTP-EQUIV={''}Content-Type{''} CONTENT={''}text/html; charset=windows-1251{''}>
<META NAME={''}title{''} CONTENT={''}Log of #forth{''}>
<META NAME={''}Author{''} CONTENT={''}exsample{''}>
<META NAME={''}Keywords{''} CONTENT={''}logs, logging, channel, irc, bot, exsample, log2html{''}>
<META NAME={''}robots{''} CONTENT= {''}index,all{''}>
<LINK REL=stylesheet TYPE={''}text/css{''} HREF={''}default.css{''}>
<LINK REL=stylesheet TYPE={''}text/css{''} HREF={''}user.css{''}>
<TITLE>Log of #forth. {s}</TITLE>
</HEAD>
<BODY class={''}logspage{''}>
<A name={''}_top{''}></A><CENTER><A href={''}index_fr.html{''}>Main page</A></CENTER><BR><HR><BR><BR>"
STYPE ;

: put-footer
" <BR><HR><BR><BR><BR><BR><CENTER><A href={''}#_top{''}>up</A></CENTER>
<hr>
<table width=100% border=#CCCCCC bgcolor=#EEEEEE>
<tr>
<td width=14%><center><a href=http://winglion.ru/irc_logs/><b>All&nbsp;Logs</b></a></td>
</tr>
</table>
<hr>

<BR><BR><BR><SPAN style={''}color:black; font-size: 0.8em;{''}>Generated by log2html for exsample</SPAN></BODY>
</HTML>" STYPE ;

0 VALUE time

: put-time ( a u -- ) time STR@ " <span class={''}time{''}>[{s}]</span>" STYPE ;
: put-nick ( a u -- ) " <span class={''}nick{''}>[{s}]</span>" STYPE ;
: put-text ( a u -- ) SPACE TYPE ;
: sput-text DUP STR@ TYPE STRFREE ;
: sput-else ( s -- ) DUP STR@ " <span class={''}else{''}> {s}</span>" STYPE STRFREE ;
: put-break ( -- ) S" <BR>" TYPE CR ;

: line=> PRO put-time CONT put-break ;

: put-message-text
   message-text irc-action?
   IF message-sender " <b>{s}</b> {s}" sput-text
   ELSE message-sender put-nick ( a u ) put-text
   THEN ;

MODULE: BOT-LOG-TALK

: PRIVMSG line=> put-message-text ;
: NICK line=> message-text message-sender " {s} changed nick to {s}" sput-else ;
: QUIT line=> message-text message-sender " {s} quit ({s})" sput-else ;
: PART line=> message-text message-target message-sender " {s} left {s} ({s})" sput-else ;
: JOIN line=> message-text message-sender " {s} joined {s}" sput-else ;

: NOTFOUND -1 THROW ;

: '' '' ;

;MODULE

: each-line ( a u -- )
   S" |" SPLIT- ENSURE " {s}" TO time
   PARSE-IRC-MSG 0= IF EXIT THEN
   GET-ORDER
   ONLY BOT-LOG-TALK
   IRC::command ['] EVALUATE CATCH IF 2DROP THEN
   SET-ORDER
;

: (log2html) { d m y -- }
   <# S" _pg1.htm" HOLDS d S>D # # 2DROP m S>D # # 2DROP y S>D #S 2DROP
      S" forth" HOLDS 0 0 #> R/W CREATE-FILE THROW TO H-STDOUT
   " {#d}/{#m}/{#y}" STR@ put-header
   <# S" .log" HOLDS d S>D # # 2DROP m S>D # # 2DROP y S>D #S 2DROP
      S" #forth." HOLDS 0 0 #>
   START{ FileLines=> DUP STR@ each-line }EMERGE
   put-footer
   H-STDOUT CLOSE-FILE THROW ;

: log2html ( d m y -- )
   H-STDOUT { H }
   ['] (log2html) CATCH H TO H-STDOUT IF CR ." ERROR!" THEN ;
