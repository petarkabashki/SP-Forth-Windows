\ Работа с матрицами ver. 1.2
\ Якимов Д. А. 8.03.99 nsoft@chat.ru
\ Формат матрицы -      4 - указатель на область памяти с матрицей
\       Область памяти: 2 - столбцов
\                       2 - строк
\                       n - тело матрицы   

REQUIRE F. lib\include\float.f

: m,
    DUP 
    F! FLOAT+
;

\ создает матрицу в памяти, на входе ( строк столбцов )
: НеименМатр ( y x -- addr2 ) \ * 
    2DUP * FLOATS CELL+ ALLOCATE THROW DUP 
    >R W!                
    R@ 2+ W!
    R>                  
;

\ компилирует матрицу - не тестировано!!!
: НеименМатр, ( -- addr )
    2DUP HERE >R * FLOATS ALLOT 
    R@ C!
    R@ 1+ C!
;

: Матрица ( row col -- ) \ * 
     CREATE HERE CELL+ , 2DUP W, W, * FLOATS ALLOT 
     DOES> @ ( D: строка столбец -- F: r )
;

: Освободить  ( id -- ) 
     FREE THROW
;

: ВзятьПараметры ( id -- row col )
    DUP 2+ W@ SWAP W@
;

: ДатьПараметры ( row col id -- )
    DUP >R W!
    R> 2+ W!
;


: Формировать ( id -- )   
     4 + 
;
: Закончить                
     DROP
;

0 VALUE Столбцов1
0 VALUE Столбцов2
0 VALUE Столбцов3
0 VALUE Строк1
0 VALUE Строк2
0 VALUE Строк3
0 VALUE РазмерМатр1
0 VALUE РазмерМатр2
0 VALUE РазмерМатр3
0 VALUE НачалоМатр1
0 VALUE НачалоМатр2
0 VALUE НачалоМатр3

: ТекущаяМатр1 ( id -- )       \ *
    DUP ВзятьПараметры
    TO  Столбцов1
    TO  Строк1
    TO  НачалоМатр1
    Столбцов1 Строк1 * FLOATS TO РазмерМатр1
;
: ТекущаяМатр2 ( id -- )       \ *
    DUP ВзятьПараметры
    TO  Столбцов2
    TO  Строк2
    TO  НачалоМатр2
    Столбцов2 Строк2 * FLOATS TO РазмерМатр2
;
: ТекущаяМатр3 ( id -- )       \ *
    DUP ВзятьПараметры
    TO  Столбцов3
    TO  Строк3
    TO  НачалоМатр3
    Столбцов3 Строк3 * FLOATS TO РазмерМатр3
;
: ИнформацияОМатр ( id -- )
    ТекущаяМатр1
    ." Строк: " Строк1 . CR
    ." Столбцов: " Столбцов1 . CR
    ." Начало: " НачалоМатр1 . CR
    ." Размер: " РазмерМатр1 . CR
;

: Матр*Число ( id1 F: r -- ) \ *
    ТекущаяМатр1
    НачалоМатр1 4 + DUP
    РазмерМатр1 + SWAP
    DO I FDUP F@ F*
       I F!
       F-SIZE @
    +LOOP
    FDROP
;
: Матр/Число ( id1 F: r -- ) \ *
    ТекущаяМатр1
    НачалоМатр1 4 + DUP
    РазмерМатр1 + SWAP
    DO I FDUP F@ FSWAP F/
       I F!
       F-SIZE @
    +LOOP
    FDROP
;

\ id1=id1+id2
: Матр+ ( id1 id2 -- )  \ *
    ТекущаяМатр2
    ТекущаяМатр1
    Столбцов1 Строк1 * 
    0 
    DO
     I FLOATS DUP НачалоМатр1 4 + + DUP F@  \ l addr F: r
     SWAP НачалоМатр2 4 + + F@ F+ F!
    LOOP
;

: GET-ITEM ( y x addr -- addr1 )  \ *
    >R
    SWAP     \ x y1
    R@ W@ * + FLOATS
    R> + 4 +
;


: ВзятьЭлемент ( y x addr -- )      \ *
    GET-ITEM  F@
;
: ДатьЭлемент ( y x addr F: r -- )   \ *
    GET-ITEM F!
;

: СтрокуНаСтолбец ( row col -- F: r ) \ *
    .E
    Столбцов1 0 
    DO
      2DUP 
      I SWAP НачалоМатр2 ВзятьЭлемент
      I НачалоМатр1 ВзятьЭлемент
      F* F+
    LOOP
    2DROP
;
\ id3=id1*id2
: Матр* ( id1 id2 id3 -- ) \ *
    ТекущаяМатр3
    ТекущаяМатр2
    ТекущаяМатр1
    Строк1 Столбцов2 НачалоМатр3 ДатьПараметры
    Столбцов2 0
    DO
      Строк1 0
      DO
         I J 2DUP 
         СтрокуНаСтолбец 
         НачалоМатр3 ДатьЭлемент
      LOOP
    LOOP
;
: ПечататьМатрицу ( id -- ) \ *
    ТекущаяМатр1
    Строк1 0
    DO
      Столбцов1 0
      DO
        J I НачалоМатр1 ВзятьЭлемент F. SPACE
      LOOP
      CR
    LOOP
;
\ id1 -> id2
: КопироватьМатр ( id1 id2 -- ) \ *
    ТекущаяМатр2
    ТекущаяМатр1
    НачалоМатр1 НачалоМатр2 Строк1 Столбцов1 * FLOATS 4 +
    CMOVE
;

: Инициализировать ( id F: r -- )  \ *
    ТекущаяМатр1
    Столбцов1 0 
    DO
      Строк1 0 
      DO
        I J FDUP НачалоМатр1 ДатьЭлемент 
      LOOP
    LOOP
    FDROP
;
: СделатьЕдиничной ( id -- ) \ *
    DUP .E Инициализировать
    ТекущаяМатр1
    Столбцов1 0
    DO
      I DUP НачалоМатр1 1.e ДатьЭлемент
    LOOP
;
: ИскатьМин ( id-matr -- F: r )  \ *
      ТекущаяМатр1
      0 0 НачалоМатр1 ВзятьЭлемент
      Столбцов1 0
      DO
        Строк1 0
        DO
          I J НачалоМатр1 ВзятьЭлемент
          FMIN
        LOOP
      LOOP
;
: ИскатьМакс ( id-matr -- F: r ) \ *
      ТекущаяМатр1
      0 0 НачалоМатр1 ВзятьЭлемент
      Столбцов1 0
      DO
        Строк1 0
        DO
          I J НачалоМатр1 ВзятьЭлемент
          FMAX
        LOOP
      LOOP
;

: НоваяМатр ( id -- addr )
        ВзятьПараметры НеименМатр 
;

: ЕденицыНадДиагональю ( id -- )
    .E DUP  Инициализировать
    ТекущаяМатр1
    Столбцов1 1 = IF EXIT 
                  ELSE Строк1 1 = IF EXIT THEN
                  THEN
    Столбцов1 1- 0
    DO
      I DUP 1+ НачалоМатр1 1.e ДатьЭлемент
    LOOP
;
    
0 VALUE Временная

\ матр id в степени u
: Матр** ( id u -- ) \ *
    DUP 1 >
    IF
      OVER DUP ВзятьПараметры НеименМатр TO Временная
      TO НачалоМатр1
      1 DO
          DUP НачалоМатр1 Временная Матр*
          Временная TO НачалоМатр1
        LOOP
      Временная SWAP КопироватьМатр
      Временная Освободить 
    ELSE 2DROP
    THEN
;
(
0 VALUE A1
0 VALUE A2
0 VALUE A3
3 2 НеименМатр TO A1 
2 3 НеименМатр TO A2
3 3 НеименМатр TO A3

A1 Формировать
   .e m, 1.e m, 
   .e m, .e m, 
   7e m, 8e m, 
Закончить

A2 Формировать
   0.1e m, 0.005e m,   7e m,
   .e m,   0.1e m,      7e m,
Закончить
: TM
    ТекущаяМатр1
    Столбцов1 0 
    DO
      0 I НачалоМатр1 I DS>F ДатьЭлемент
    LOOP
;

PRINT-EXP
4 FFORM !
A1 ПечататьМатрицу
CR 
A2 ПечататьМатрицу
)

(
Нужна была библиотека работы с матрицами - она перед вами.
Это настоящий шедевр.
Объект - указатель на область памяти матрицы.
Пример использования - в скобках.
Обратите внимание на реализацию процедуры умножения - можно пользоваться 
вспомогательными матрицами любого размера.
С матрицами можно делать все что душе Вашей угодно, даже возводить в 
степень. Этот файл - наглядный пример преимущества ФОРТа над с++
(взгляните на его размер и имена операторов - в с++ об этом можно было 
только мечтать
Чрезвычайная удобство в отладке и пр.
Кто пользуется - напишите впечатления, пожелания, предложения...

ver 1.1 - переделано Инициализировать
ver 1.2 - добавлены ИскатьМин и ИскатьМакс
)