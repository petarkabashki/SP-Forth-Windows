\ Перехват всех видов компиляции в кодофайл

\ Опеределяется болванка DP-HOOK куда можно записать
\ желаемые действия по проверке идущей компиляции.

\ Хаковатая штука. И то спасибо надо сказать что в SPF
\ DP -- не переменная, а "умное" определение, которое
\ ещё можно подменить "на лету".

REQUIRE /TEST ~profit/lib/testing.f
REQUIRE NEW-STORAGE ~pinka/spf/storage.f
 \ работает и без storage.f, но если он будет включён _позже_ -- будут проблемы
REQUIRE R@ENTER, ~profit/lib/bac4th.f
REQUIRE REPLACE-WORD lib/ext/patch.f

MODULE: dp-hook

\ Берёт из потока два имени. С первым создаёт новое
\ определение. У второго имени находит содержимое
\ его кода и _копирует_ его в новое слово.
\ Относительные переходы в командах jmp и call (если есть)
\ ломаются, кроме самого первого CALL который исправляется вручную
: DUPLICATE
HEADER '
\ хак номер 1: наглое копирование килобайта участка кода
DUP HERE 2DUP >R >R 1000 DUP ALLOT CMOVE
\ хак номер 2: если первая команда CALL, то исправляем адрес (для "совместимости" с ~pinka\spf\storage.f)
R> DUP C@ 0xE8 = IF 1+ DUP @ + R@ 1+ TUCK - SWAP ! ELSE DROP THEN RDROP DROP ;

DUPLICATE DP1 DP \ копируем содержимое DP в DP1 , так как DP мы сейчас сломаем

EXPORT

' NOOP ->VARIABLE &DP-HOOK \ переменная хранит адрес кода обработчика компиляции
\ xt в &DP-HOOK -- перехватчик компиляции, в нём можно делать всякие проверки/оптимизации

\ Использовать перекрываемое определение (scattered colon)
\ технически много сложнее, поэтому сделано так. Хотя можно 
\ было бы и векторное определение, но схема 
\ переменная-хранилище/вызывающее-слово удобнее для bac4th'а
\ из-за вложенности и откатываемости (см. ~profit/lib/compile2Heap.f)
:NONAME &DP-HOOK @ >R ['] NOOP &DP-HOOK ! [ R@ENTER, ]  R> &DP-HOOK ! DP1 ; ' DP REPLACE-WORD
\ Обратите внимание что во время запуска обработчика перехват
\ снимается. Это сделано для того чтобы можно было безболезненно
\ использовать HERE и прочее в самом DP-HOOK

: SET-DP-HOOK ( xt --> \ <-- ) PRO  &DP-HOOK KEEP!  CONT ; \ включить на время работы определения xt как перехватчик компиляции
: DIS-DP-HOOK ( --> \ <-- ) PRO  ['] NOOP &DP-HOOK KEEP!  CONT ; \ отключить все перехватчики компиляции на время работы определения

;MODULE

/TEST
:NONAME ." ." ; &DP-HOOK !

$> 1 ,
$> : r DUP ;

:NONAME HERE . ; &DP-HOOK !


$> 1 ,
$> : r1 DUP ;